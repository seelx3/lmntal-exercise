/* union find (disjoint set) */
%%% init
make(X) :- new($x), new($y) | rt($x),n(X,$y,$x).

%%% merge
merge(X1,X2), mutex :- mg(check(X1,X2)).
mg(isSame(rootis(X1),rootis(X2))) :-
  X1==X2 | mutex.
mg(isSame(rootis(X1),rootis(X2)))  :-
  X1\==X2 | link(X1,X2).
link(X1,X2), n(Y1,!C1,!P1), n(Y2,!C2,!P2), rt(!L) :- 
  X1==Y1, X2==Y2, !L==!P2 | n(X1,!C1,!P1), n(X2,!C2,!C1), mutex.

%%% isSame
check(X1,X2,L) :- int(X1), int(X2) | isSame(getroot(X1),getroot(X2),L).
% tracing parent node
getroot(X, L), n(X0, !C0, !P0), n(X2, !C2, !P2) \ n(X1, !C1, !P1) :-
  X==X2, !C1==!P2, !C0==!P1, new($x) | n(X1, $x, !P1), !P1><!C1.
gr1 @@
rt(!C0), n(X1, !C1, !P1), n(X2, !C2, !P2) \ getroot(X, L) :-
  int(X1), X==X2, !C1==!P2, !C0==!P1 | rootis(X1, L).
gr2 @@
rt(!C0), n(X1,!C1,!P1) \ getroot(X,L) :- 
  X==X1, !C0==!P1 | rootis(X,L).
/* end of union find */

% operations
mutex.
make(0),make(1),make(2),make(3).
merge(0, 1).
merge(2, 3).
merge(1, 3).
merge(3, 1).